#! /usr/bin/fish

argparse --name=nsend -x bootstrap,received -N 1 's/ssh=' 'd/dest=' 'b/bootstrap' 'r/received=' 'p/progress=?' 'n/dry-run' -- $argv

set target (string trim -rc / $_flag_d)

if set -q _flag_p
    echo "using 'pv -W$_flag_p' to show progress"
end

if set -q _flag_s
    ssh -o ConnectTimeout=4 -i /home/micke/.ssh/id_rsa $_flag_s test -d $target
    switch $status
    case 0
        set ssh "ssh -i /home/micke/.ssh/id_rsa $_flag_s sudo -n"
    case 255
        exit 1
    case '*'
        echo "Could not find '$target' on '$_flag_s'."
        exit 1
    end
else
    set ssh ""
end

if not set -q _flag_d
    echo "missing destination, did you mean '--dest $argv[1] $argv[2]..'?" >&2
    exit 1
end

if set -q _flag_r
    echo "'--received' argument not yet implemented.." >&2
    exit 1
end


for snap in $argv

    if not test -e $snap
        echo "'$snap' does not exist" >&2
        exit 1
    end

    if set -q _flag_p
        test -f size.$snap && set size (cat size.$snap)
        if test $size -gt 0
            set pipe "| pv -W$_flag_p -s $size |"
        else
            set pipe "| pv -W$_flag_p |"
        end
    else
        set pipe "|"
    end

    set vol (string split . $snap)[1]
    
    if set -q _flag_n
        # Use local head on dry-run
        set head $vol.head
    else if set -q _flag_s
        # We can't use symlinks over ssh
        set head $vol.ssh.$_flag_s
    else
        set head $target/$vol.received
    end

    if not set -q _flag_b
    and not test -L $head
        echo "'$head' is not a symbolic link." >&2
        if test -L $vol.head
            read -lP "Do you want to use '$vol.head' instead? [y/N] " ok
            switch $ok
                case Y y J j
                    set head $vol.head
                case N n ''
                    exit 1
            end
        else
            echo "'$vol.head' is not a symbolic link either." >&2
            exit 1
        end
    end
    
    if test -L $head
        if set -q _flag_n
            eval "btrfs send -p $head $snap $pipe wc -c > size.$snap"
        else
            eval btrfs send -p $head $snap $pipe $ssh btrfs receive $target
        end
    else if set -q _flag_b
        echo "Bootstrapping by sending $snap..." >&2

        if set -q _flag_n
            eval "btrfs send $snap $pipe wc -c > size.$snap"
        else
            eval btrfs send $snap $pipe $ssh btrfs receive $target
        end

        ln -sfT $snap $vol.head
        if not set -q _flag_n
            if set -q _flag_s
                ln -sfT (pwd)/$snap $vol.ssh.$_flag_s
            else
                ln -sfT (pwd)/$snap $target/$vol.received
            end
        end
    else
        echo "'$head' is not a symbolic link." >&2
        exit 1
    end
    
    set succ $status
    if test $succ -ne 0
        exit $succ
    end

    ln -sfT $snap $vol.head
    if not set -q _flag_n
        if set -q _flag_s
            ln -sfT (pwd)/$snap $vol.ssh.$_flag_s
        else
            ln -sfT (pwd)/$snap $target/$vol.received
        end
    end

    set vols $vols $vol

end

if set -q _flag_s
or set -q _flag_n
    exit 0
end

set weekstamp (date +%Y-v%V-%u-%a-%H.%M.%S)

for vol in $vols

    if not test -d $vol
        echo "\"$vol\" is not a directory."
        exit 1
    end

    if not test -d @heads
        echo "@heads is not a directory."
        exit 1
    end
    
    set head $vol.head
    set snap "@heads/$vol.head.$weekstamp"

    if not test -e $snap
        btrfs subvolume snapshot -r $vol $snap
        btrfs send -c $head $snap | btrfs receive $target
    end
end
