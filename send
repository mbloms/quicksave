#! /usr/bin/fish

argparse --name=nsend -x bootstrap,received -N 1 'd/dest=' 'b/bootstrap' 'r/received=' -- $argv

if not set -q _flag_d
    echo "missing destination, did you mean '--dest $argv[1] $argv[2]..'?" >&2
    exit 1
end

if set -q _flag_r
    echo "'--received' argument not yet implemented.." >&2
    exit 1
end

set target (string trim -rc / $_flag_d)

if set -q _flag_b
    echo bootstrap = $_flag_b
end

for snap in $argv

    set vol (string split . $snap)[1]
    
    set head $target/$vol.received

    if not test -s $head
        echo "'$head' is not a symbolic link." >&2
        set head $vol.head
        echo "Trying $head instead." >&2
    end
    
    if test -s $head
        btrfs send -p $head $snap | btrfs receive $target
    else if set -q _flag_b
        echo "Bootstrapping by sending $snap..." >&2
        btrfs send $snap | btrfs receive $target
    else
	echo "'$head' is not a symbolic link." >&2
        exit 1
    end
    
    set succ $status
    if test $succ -ne 0
        exit $succ
    end

    ln -sfT $snap $vol.head
    ln -sfT (pwd)/$snap $target/$vol.received

    set vols $vols $vol

end

set weekstamp (date +%Y-v%V-%u-%a-%H.%M.%S)

for vol in $vols

    if not test -d $vol
        echo "\"$vol\" is not a directory."
        exit 1
    end

    if not test -d @heads
        echo "@heads is not a directory."
        exit 1
    end
    
    set head $vol.head
    set snap "@heads/$vol.head.$weekstamp"

    if not test -e $snap
        btrfs subvolume snapshot -r $vol $snap
        btrfs send -p $head $snap | btrfs receive $target
    end
end
