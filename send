#! /usr/bin/fish

set target $argv[1]

set --erase argv[1]

for snap in $argv

    set vol (string split . $snap)[1]
    
    set head $target/$vol.received

    if not test -s $head
        # Borde va stderr
        echo "\"$head\" is not a symbolic link."
        set head $vol.head
        echo "Trying $head instead."
    end
    
    if not test -s $head
        echo "\"$head\" is not a symbolic link."
        exit 1
    end
    
    btrfs send -p $head $snap | btrfs receive $target
    set succ $status
    if test $succ -ne 0
        exit $succ
    end

    ln -sfT $snap $vol.head
    ln -sfT (pwd)/$snap $target/$vol.received

    set vols $vols $vol

end

set weekstamp (date +%Y-v%W-%u-%A-%H.%M.%S)

for vol in $vols

    if not test -d $vol
        echo "\"$vol\" is not a directory."
        exit 1
    end

    if not test -d @heads
        echo "@heads is not a directory."
        exit 1
    end
    
    set head $vol.head
    set snap "@heads/$vol.head.$weekstamp"

    if not test -e $snap
        btrfs subvolume snapshot -r $vol $snap
        btrfs send -p $head $snap | btrfs receive $target
    end
end